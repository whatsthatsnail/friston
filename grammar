Syntax:
    CAPITALIZE terminals with variable literals
    "quote" terminals with exact lexemes (no literal)
    | or
    * zero or more
    ()? optional

program         -> declaration* EOF ;

declaration     -> varDecl
                | statement ;

varDecl         -> "var" IDENTIFIER ( "=" expression )? ";" ;

statement       -> exprStmt
                | ifStmt
                | printStmt 
                | block ;

block           -> "{" declaration* "}" ;

exprStmt        -> expression ";" ;
ifStmt          -> "if" "(" expression ")" statement ( "else" statement )?
printStmt       -> "print" expression ";" ;

expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment
                | equality
equality        -> comparison ( ("==" | "!=") comparison)* ;
comparison      -> addition ( ("<" | "<=" | ">" | ">=") addition)* ;
addition        -> multiplication ( ("+" | "-") multiplication )* ;
multiplication  -> unary ( ("*" | "/") unary )* ;
unary           -> ("-" | "!") unary
                | primary ;

primary         -> NUMBER | STRING | "true" | "false" | "nil"
                | "(" expression ")"
                | IDENTIFIER ;
